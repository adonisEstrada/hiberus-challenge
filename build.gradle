plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.14'
    id 'org.openapi.generator' version '7.2.0'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.hiberus.paymentinitiation.infrastructure.adapters.api.generated"
    modelPackage = "com.hiberus.paymentinitiation.infrastructure.adapters.api.dtos.generated"
    configOptions = [
        useSpringBoot3: "true",
        useTags: "true"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/src/main/java"
        }
    }
}

bootJar {
    mainClass = 'com.hiberus.paymentinitiation.PaymentInitiationApplication'
}

group = 'com.hiberus'
version = '0.0.1'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'io.rest-assured:rest-assured'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    toolVersion = '11.0.0'
    enableExternalDtdLoad = false
}

spotbugs {
    toolVersion = '4.8.3'
    reportsDir = file("$buildDir/reports/spotbugs")
    effort = 'max'
    reportLevel = 'high'
}
